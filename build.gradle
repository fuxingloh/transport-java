buildscript {
    repositories {
        mavenCentral()
    }
    dependencies {
        classpath "io.codearte.gradle.nexus:gradle-nexus-staging-plugin:0.5.3"
    }
}
apply plugin: 'io.codearte.nexus-staging'

subprojects {
    apply plugin: 'idea'
    apply plugin: 'java'
    apply plugin: 'signing'
    apply plugin: 'maven'

    group 'dev.fuxing'
    version '1.0-SNAPSHOT'

    ext.jUnitVersion = '5.4.2'
    ext.guiceVersion = '4.2.2'

    sourceCompatibility = 1.11
    targetCompatibility = 1.11

    compileJava.options.encoding = 'UTF-8'
    compileTestJava.options.encoding = 'UTF-8'

    dependencies {
        testImplementation group: 'org.junit.jupiter', name: 'junit-jupiter-api', version: jUnitVersion
        testImplementation group: 'org.junit.jupiter', name: 'junit-jupiter-api', version: jUnitVersion
        testImplementation group: 'org.junit.jupiter', name: 'junit-jupiter-params', version: jUnitVersion

        testRuntimeOnly group: 'org.junit.jupiter', name: 'junit-jupiter-engine', version: jUnitVersion

        compile group: 'com.google.code.findbugs', name: 'jsr305', version: '3.0.2'
    }

    repositories {
        mavenCentral()
        maven {
            url "${nexus_contextUrl}/repository/v22/"
            credentials {
                username = "${nexus_user}"
                password = "${nexus_password}"
            }
        }
    }

    test {
        useJUnitPlatform()
    }

    task javadocJar(type: Jar) {
        classifier = 'javadoc'
        from javadoc
    }

    task sourcesJar(type: Jar) {
        classifier = 'sources'
        from sourceSets.main.allSource
    }

    artifacts {
        archives javadocJar, sourcesJar
    }

    signing {
        sign configurations.archives
    }

    // ./gradlew uploadArchives
    // OSSRH publication
    uploadArchives {
        repositories {
            mavenDeployer {
                // POM signature
                beforeDeployment { MavenDeployment deployment -> signing.signPom(deployment) }
                // Target repository
                repository(url: "https://oss.sonatype.org/service/local/staging/deploy/maven2/") {
                    authentication(userName: ossrhUser, password: ossrhPassword)
                }
                snapshotRepository(url: "${nexus_contextUrl}/repository/v22-snapshots/") {
                    authentication(userName: nexus_user, password: nexus_password)
                }
                pom {
                    artifactId = rootProject.name + '-' + project.name

                    project {
                        name 'transport-' + project.name
                        description 'A very very very opinionated JSON communication protocol between your services.'
                        packaging 'jar'
                        url 'https://github.com/fuxingloh/transport-java'

                        scm {
                            connection 'scm:git:https://github.com/fuxingloh/transport-java.git'
                            developerConnection 'scm:git:git@github.com:fuxingloh/transport-java.git'
                            url 'https://github.com/fuxingloh/transport-java.git'
                        }

                        licenses {
                            license {
                                name 'Apache License 2.0'
                                url 'https://github.com/fuxingloh/transport-java/blob/master/LICENSE'
                                distribution 'repo'
                            }
                        }

                        developers {
                            developer {
                                id = 'fuxing'
                                name = 'Fuxing Loh'
                                email = 'fuxing@fuxing.dev'
                            }
                        }
                    }
                }
            }
        }
    }
}

nexusStaging {
    username = ossrhUser
    password = ossrhPassword
}